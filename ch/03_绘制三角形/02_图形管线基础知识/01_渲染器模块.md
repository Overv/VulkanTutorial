与早期的 API 不同，Vulkan 中的渲染器程序代码是以字节码形式使用的，而不是像 [GLSL](https://en.wikipedia.org/wiki/OpenGL_Shading_Language)和 [HLSL](https://en.wikipedia.org/wiki/High-Level_Shading_Language)，这样的人类可读语法程序。 这种字节码格式称为 [SPIR-V]https://www.khronos.org/spir)，能同时在Vulkan和 OpenCL(均为 Khronos API) 中使用。它是一种可用于编写图形和计算渲染器的代码形式。在本教程中，我们将重点介绍 Vulkan 图形管道中使用的渲染器。

使用字节码程序代码的优势在于 ，GPU 供应商编写的渲染器编译器将字节码代码转换为本机可运行机器指令的复杂性要低得多。 过去表明，对于像 GLSL 这样的人类可读语法，一些GPU 供应商对标准的解释并不统一。 如果您碰巧使用不同供应商的 GPU，当编写重要的渲染器时，可能会因为供应商的驱动程序差异导致代码语法错误的风险，有可能更糟糕的是，您的渲染器会因为编译器错误而运行得到不同效果。 使用像 SPIR-V 这样的简单字节码格式，有望避免。

然而，这并不意味着我们需要手动编写这个字节码。Khronos 发布了他们自己的独立于供应商的编译器，可将 GLSL 编译为 SPIR-V。此编译器旨在验证您的着色器代码是否完全符合标准，并生成一个可以随程序一起提供的 SPIR-V 二进制文件。您还可以将此编译器作为库包含在运行时生成 SPIR-V，但我们不会在本教程中这样做。 虽然我们可以通过 glslangValidator.exe 直接使用这个编译器，但我们将使用 Google 的 glslc.exe 代替。 `glslc` 的优点是它使用与众所周知的编译器（如 GCC 和 Clang）相同的参数格式，并包含一些额外的功能，如 *includes*。 它们都已包含在 Vulkan SDK 中，因此您无需下载任何额外内容。

GLSL 是一种具有 C 风格语法的渲染语言。用它编写的程序有一个“main”函数被对应管道过程对象调用。GLSL 不使用输入参数和返回值作为输出，而是使用全局变量来处理输入和输出。该语言包括许多有助于图形编程的功能，例如内置向量和矩阵基元。包括叉积、矩阵向量积和向量周围的反射等运算的函数。向量类型称为“vec”，带有一个表示元素数量的数字。例如，3D 位置将存储在 `vec3` 中。可以通过 .x 之类的成员访问单个组件，但也可以同时从多个组件创建一个新向量。例如，表达式 `vec3(1.0, 2.0, 3.0).xy` 将导致 `vec2`。向量的构造函数也可以采用向量对象和标量值的组合。例如，一个 `vec3` 可以用 `vec3(vec2(1.0, 2.0), 3.0)` 构造。

正如上一章所提到的，我们需要编写一个顶点渲染器和一个片段渲染器来获得屏幕上的一个三角形。接下来的两节将分别介绍 GLSL 代码，然后我将向您展示如何生成两个 SPIR-V 二进制文件并将它们加载到程序中。

## 顶点渲染器

顶点渲染器处理每个传入的顶点。它将其属性（如世界位置、颜色、法线和纹理坐标）作为输入。 输出是剪辑坐标中的最终位置以及需要传递给段渲染器的属性，例如颜色和纹理坐标。 然后，这些值将由光栅化器在片段上进行插值，以产生平滑的渐变。

*剪辑坐标*是来自顶点渲染器的四维向量，随后通过将整个向量除以其最后一个分量将其转换为*标准化设备坐标*。这些标准化的设备坐标是 [homogeneous coordinates] (https://en.wikipedia.org/wiki/Homogeneous_coordinates)，将帧缓冲区映射到 [-1, 1] x [-1, 1] 坐标系，如下所示 ：

![](/images/normalized_device_coordinates.svg)

如果您以前涉足计算机图形学，那么您应该已经熟悉这些。 如果您以前使用过 OpenGL，那么您会注意到
现在翻转 Y 坐标的符号。 Z 坐标现在使用与 Direct3D 中相同的范围，从 0 到 1。

对于我们的第一个三角形，我们不会应用任何形状变换，我们只需将三个顶点的位置直接指定为标准化设备
坐标以创建以下形状：

![](/images/triangle_coordinates.svg)

我们可以直接输出归一化的设备坐标，方法是将它们作为裁剪坐标系坐标从顶点着色器输出，最后一个分量设置为“1”。 这样，将裁剪坐标系坐标转换为标准化设备坐标系坐标的齐次坐标归一化不会改变任何坐标值。

通常这些坐标将存储在顶点缓冲区中，但在 Vulkan 中创建顶点缓冲区并用数据填充它并非易事。 因此，我决定暂不使用顶点缓冲区，仅通过简单方式绘制一个三角形并在屏幕上弹出。 我们使用的简单方法是将坐标直接包含在顶点渲染器中。 代码如下所示：

```glsl
#version 450

vec2 positions[3] = vec2[](
    vec2(0.0, -0.5),
    vec2(0.5, 0.5),
    vec2(-0.5, 0.5)
);

void main() {
    gl_Position = vec4(positions[gl_VertexIndex], 0.0, 1.0);
}
```

每个顶点将调用 `main` 函数。内置的 `gl_VertexIndex` 变量包含当前顶点的索引。这通常是顶点缓冲区的索引，但在我们的例子中，它将是顶点数据硬编码数组的索引。 每个顶点的位置是从渲染器中的常量数组访问的，并与常量“z”和“w”分量组合以产生剪辑坐标系中的位置。内置变量 `gl_Position` 用作输出。

## 段渲染器

由顶点渲染器的位置形成的三角形将用段渲染器填充屏幕上的一个区域。在这些片段上调用段渲染器以生成帧缓冲区的颜色和深度。 为整个三角形输出红色的简单段渲染器如下所示：

```glsl
#version 450

layout(location = 0) out vec4 outColor;

void main() {
    outColor = vec4(1.0, 0.0, 0.0, 1.0);
}
```

`main` 函数将被每个渲染片段调用，就像顶点着色器 `main` 函数被每个顶点调用一样。 GLSL中的颜色是 4 分量向量，其 R、G、B 和 alpha 通道取值都在 [0, 1] 范围内。与顶点着色器中的 `gl_Position` 不同，没有内置变量来输出当前片段的颜色。 您必须为每个帧缓冲区指定自己的输出变量，其中 `layout(location = 0)` 修饰符指定帧缓冲区的索引。 红色被写入此 `outColor` 变量，该变量链接到索引 `0` 处的第一个（也是唯一的）帧缓冲区。

## 为每个顶点赋予颜色

把整个三角形变成红色不是很有趣，下面的彩色三角形会不会更漂亮？

![](/images/triangle_coordinates_colors.png)

我们必须对两个渲染器进行一些更改才能完成此操作。首先，我们需要为三个顶点中的每一个顶点指定不同的颜色。顶点渲染器现在应该包含一个带有颜色的数组，就像位置对应的数组一样：

```glsl
vec3 colors[3] = vec3[](
    vec3(1.0, 0.0, 0.0),
    vec3(0.0, 1.0, 0.0),
    vec3(0.0, 0.0, 1.0)
);
```

现在我们只需要将这些每个顶点的颜色传递给段渲染器，这样段渲染器就可以将它们的插值结果输出到帧缓冲区。将颜色的输出添加到顶点渲染器并写入 `main` 函数：

```glsl
layout(location = 0) out vec3 fragColor;

void main() {
    gl_Position = vec4(positions[gl_VertexIndex], 0.0, 1.0);
    fragColor = colors[gl_VertexIndex];
}
```

接下来，我们需要在片段着色器中添加匹配的输入：

```glsl
layout(location = 0) in vec3 fragColor;

void main() {
    outColor = vec4(fragColor, 1.0);
}
```

段渲染器的输入变量不一定必须使用与顶点渲染器相同的名称，它们将使用 `location` 指令指定的索引连接在一起。 `main` 函数已修改为输出颜色和 alpha 值。 如上图所示，"fragColor"的值将自动为三个顶点之间的片段进行插值，从而产生平滑的渐变。

## 编译渲染器

在项目的根目录中创建一个名为“shaders”的目录，并将顶点渲染器存储在一个名为“shader.vert”的文件中，并将片段渲染器存储在该目录中的一个名为“shader.frag”的文件中。GLSL渲染器没有官方的扩展名，但这两个通常用来区分它们。

'shader.vert'内容如下：

```glsl
#version 450

layout(location = 0) out vec3 fragColor;

vec2 positions[3] = vec2[](
    vec2(0.0, -0.5),
    vec2(0.5, 0.5),
    vec2(-0.5, 0.5)
);

vec3 colors[3] = vec3[](
    vec3(1.0, 0.0, 0.0),
    vec3(0.0, 1.0, 0.0),
    vec3(0.0, 0.0, 1.0)
);

void main() {
    gl_Position = vec4(positions[gl_VertexIndex], 0.0, 1.0);
    fragColor = colors[gl_VertexIndex];
}
```

'shader.frag'内容如下：

```glsl
#version 450

layout(location = 0) in vec3 fragColor;

layout(location = 0) out vec4 outColor;

void main() {
    outColor = vec4(fragColor, 1.0);
}
```

我们将使用'glslc'程序对上述渲染器程序进行编译。

**Windows**

创建一个包含以下内容的'compile.bat'文件：

```bash
C:/VulkanSDK/x.x.x.x/Bin32/glslc.exe shader.vert -o vert.spv
C:/VulkanSDK/x.x.x.x/Bin32/glslc.exe shader.frag -o frag.spv
pause
```

将"glslc.exe"的路径替换为您安装 Vulkan SDK 的路径。双击该文件以运行它。

**Linux**

创建一个包含以下内容的'compile.sh'文件：

```bash
/home/user/VulkanSDK/x.x.x.x/x86_64/bin/glslc shader.vert -o vert.spv
/home/user/VulkanSDK/x.x.x.x/x86_64/bin/glslc shader.frag -o frag.spv
```

将"glslc"的路径替换为您安装的路径Vulkan SDK。 使用 `chmod +x compile.sh` 更改脚本可执行权限，然后运行它。

**面向不同平台的结束指令**

这两个命令告诉编译器读取 GLSL 源文件并使用 `-o`（输出）标志输出一个 SPIR-V 字节码文件。

如果您的着色器包含语法错误，那么编译器会按照您的预期告诉您错误行号和问题。 例如，尝试省略分号并再次运行编译脚本。您还可以尝试不带任何参数运行编译器，以查看编译器支持哪些类型的参数标志。例如，它还可以将字节码输出为人类可读的格式，这样您就可以准确地看到渲染器正在做什么以及在此阶段使用的任何优化。

在命令行上编译渲染器是最直接的选项之一，也是我们将在本教程中使用方法，但也可以直接从您自己的代码编译渲染器。 Vulkan SDK 包括 [libshaderc] https://github.com/google/shaderc)，它是一个库，用于从您的程序中将 GLSL 代码编译为 SPIR-V。

## 加载渲染器

现在我们有了一种生成 SPIR-V 渲染器的方法，是时候将它们加载到我们的程序中，以便在某个时候将它们插入到图形渲染管道中。 我们将首先编写一个简单的辅助函数来从文件中加载二进制数据。

```c++
#include <fstream>

...

static std::vector<char> readFile(const std::string& filename) {
    std::ifstream file(filename, std::ios::ate | std::ios::binary);

    if (!file.is_open()) {
        throw std::runtime_error("failed to open file!");
    }
}
```

`readFile` 函数将从指定文件中读取所有字节，并将它们返回到由 `std::vector` 管理的字节数组中。 我们首先使用两个参数标记打开件：

* `ate`: 从文件末尾开始阅读
* `binary`: 将文件读取为二进制文件（避免文本转换）

在文件末尾开始读取的好处是我们可以使用读取位置来确定文件的大小并分配缓冲区：

```c++
size_t fileSize = (size_t) file.tellg();
std::vector<char> buffer(fileSize);
```

之后，我们可以回到文件的开头并一次读取所有字节：

```c++
file.seekg(0);
file.read(buffer.data(), fileSize);
```

最后关闭文件并返回字节：

```c++
file.close();

return buffer;
```

我们现在将从函数`createGraphicsPipeline`调用这个文件读取函数来加载两个渲染器的字节码：

```c++
void createGraphicsPipeline() {
    auto vertShaderCode = readFile("shaders/vert.spv");
    auto fragShaderCode = readFile("shaders/frag.spv");
}
```

通过打印缓冲区的大小并检查它们是否与实际文件大小（以字节为单位）匹配，确保正确加载渲染器。 请注意，代码不需要以空值结尾，因为它是二进制代码，我们稍后将明确其大小。

## 创建渲染器模块

在我们可以将代码传递给管道之前，我们必须将它包装在一个`VkShaderModule` 对象。 让我们创建一个辅助函数 createShaderModule` 来做到这一点。

```c++
VkShaderModule createShaderModule(const std::vector<char>& code) {

}
```

该函数将使用字节码缓存作为参数，并从中创建一个`VkShaderModule`。

创建渲染器模块很简单，我们只需要将字节码缓存指针和缓存长度值填入结构体即可。 对应的结构体参数类型为 `VkShaderModuleCreateInfo` 。需要注意的是字节码的大小以字节为单位指定，而字节码缓存指针是 `uint32_t` 指针而不是 `char` 指针。 因此，我们需要使用 `reinterpret_cast` 来转换指针，如下所示。 当您执行这样的转换时，您还需要确保数据满足 `uint32_t` 的对齐要求。 幸运的是，数据存储在“std::vector”中，默认分配器已经确保数据满足最坏情况的对齐要求。

```c++
VkShaderModuleCreateInfo createInfo{};
createInfo.sType = VK_STRUCTURE_TYPE_SHADER_MODULE_CREATE_INFO;
createInfo.codeSize = code.size();
createInfo.pCode = reinterpret_cast<const uint32_t*>(code.data());
```

然后可以通过调用 `vkCreateShaderModule` 来创建 `VkShaderModule`：

```c++
VkShaderModule shaderModule;
if (vkCreateShaderModule(device, &createInfo, nullptr, &shaderModule) != VK_SUCCESS) {
    throw std::runtime_error("failed to create shader module!");
}
```

渲染器模块对象创建输入参数与之前的对象创建函数中的参数相同：逻辑设备、创建信息结构的指针、指向自定义分配器的可选指针和句柄输出变量。 创建渲染器模块后，可以立即释放包含代码的缓冲区。 不要忘记返回创建的渲染器模块：

```c++
return shaderModule;
```

渲染器模块只是我们之前从文件中加载的渲染器字节码和其中定义的函数的一个轻量包装器。 在创建图形管道之前，SPIR-V 字节码没有进行编译和链接，也不会转换为机器代码在GPU中执行。这意味着一旦管道创建完成，我们就可以再次销毁渲染器模块，这就是为什么我们将在 createGraphicsPipeline 函数中将它们设为局部变量而不是类成员：

```c++
void createGraphicsPipeline() {
    auto vertShaderCode = readFile("shaders/vert.spv");
    auto fragShaderCode = readFile("shaders/frag.spv");

    VkShaderModule vertShaderModule = createShaderModule(vertShaderCode);
    VkShaderModule fragShaderModule = createShaderModule(fragShaderCode);
```

然后，清理应该在函数的末尾通过添加两个调用 `vkDestroyShaderModule` 来进行。本章中所有剩余的代码都将插入到这些行之前。

```c++
    ...
    vkDestroyShaderModule(device, fragShaderModule, nullptr);
    vkDestroyShaderModule(device, vertShaderModule, nullptr);
}
```

## 渲染器在图形管道中的使用

要实际使用渲染器，我们需要通过 `VkPipelineShaderStageCreateInfo` 结构将它们分配给特定的管道阶段，作为实际管道创建过程的一部分。

我们将从填充顶点渲染器的结构开始，再次在`createGraphicsPipeline` 函数完善图形渲染管道的信息填充。

```c++
VkPipelineShaderStageCreateInfo vertShaderStageInfo{};
vertShaderStageInfo.sType = VK_STRUCTURE_TYPE_PIPELINE_SHADER_STAGE_CREATE_INFO;
vertShaderStageInfo.stage = VK_SHADER_STAGE_VERTEX_BIT;
```

其中，`sType`确认定义的参数类型，'stage'告诉 Vulkan 渲染器将在哪个管道阶段使用。上一章中描述的每个可编程阶段都有一个枚举值。

```c++
vertShaderStageInfo.module = vertShaderModule;
vertShaderStageInfo.pName = "main";
```

接下来的两个成员参数指定包含代码的渲染器模块，以及要调用的入口函数，称为*入口点*。 这意味着可以将多个片段渲染器组合到一个渲染器模块中，并使用不同的入口点来区分它们的行为。 但是，在一般情况下，我们将坚持使用标准的 `main`函数作为入口点。

还有一个（可选）成员，`pSpecializationInfo`，我们不会在这里使用，但它值得进一步说明。 它允许您指定渲染器常量的值。 您可以使用单个着色器模块，通过为其中使用的常量指定不同的值，可以在创建管道时配置其行为。 这比在渲染时使用变量配置渲染器更有效，因为编译器可以进行优化，例如消除依赖于这些值的“if”语句。 如果您没有任何类似的常量，那么您可以将成员设置为 `nullptr`，我们的结构初始化会自动执行此操作。

修改结构体成员变量以适应片段渲染器很容易，如下所示：

```c++
VkPipelineShaderStageCreateInfo fragShaderStageInfo{};
fragShaderStageInfo.sType = VK_STRUCTURE_TYPE_PIPELINE_SHADER_STAGE_CREATE_INFO;
fragShaderStageInfo.stage = VK_SHADER_STAGE_FRAGMENT_BIT;
fragShaderStageInfo.module = fragShaderModule;
fragShaderStageInfo.pName = "main";
```

最后定义一个包含这两个结构体变量的数组，稍后我们将在实际的管道创建步骤中使用它来引用它们。

```c++
VkPipelineShaderStageCreateInfo shaderStages[] = {vertShaderStageInfo, fragShaderStageInfo};
```

这就是管道中的可编程阶段的全部描述内容。 下一章，我们将讲解管道中的固定功能阶段。

[C++ code](/code/09_shader_modules.cpp) /
[Vertex shader](/code/09_shader_base.vert) /
[Fragment shader](/code/09_shader_base.frag)
